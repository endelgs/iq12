<?xml version="1.0" encoding="utf-8"?>
<mx:Application xmlns:mx="http://www.adobe.com/2006/mxml" layout="vertical" creationComplete="init()" width="800" height="600" backgroundColor="#ffffff" backgroundGradientAlphas="[1.0, 1.0]" backgroundGradientColors="[#FFFFFF, #FFFFFF]" horizontalScrollPolicy="off" horizontalAlign="left">
	<mx:Metadata>
		[Event(name='dataLoaded',type="flash.events.Event")]
	</mx:Metadata>
	<mx:Style>
        DataTip 
        {
            fontFamily: "Verdana";
            fontSize: 12; 
            fontWeight:bold;
        }

    </mx:Style>
	<mx:Script>
		<![CDATA[

			import flash.net.navigateToURL;
			import mx.graphics.codec.JPEGEncoder;
			import mx.graphics.ImageSnapshot;
			import mx.collections.Sort;
			import mx.utils.ObjectUtil;
			import mx.controls.Alert;
			import mx.charts.Legend;
			import mx.rpc.events.ResultEvent;
			import eccen.graphs.HTTPUtil;
			import mx.events.ListEvent;
			
			import eccen.graphs.EPlotChart;
			import eccen.graphs.EAreaChart;
			import eccen.graphs.EBubbleChart;
			import eccen.graphs.ELineChart;
			import eccen.graphs.EColumnChart;
			import eccen.graphs.EBarChart;
			import eccen.graphs.EPieChart;

			import mx.collections.ArrayCollection;
			
			[Bindable] private var chartType:ArrayCollection = new ArrayCollection();
			[Bindable] private var seriesMeta:ArrayCollection = new ArrayCollection();
			[Bindable] private var fieldsMeta:ArrayCollection = new ArrayCollection();
			
			[Bindable] private var mainAxisField:String = ''; 		//eixo das categorias
			[Bindable] private var mainAxisLabel:String = '';
			[Bindable] private var secondaryAxisFields:ArrayCollection = new ArrayCollection(); 	// eixo de quantidade
			
			[Bindable] private var chartTitle:String = 'Título';
			[Bindable] private var chartDesc:String = 'Descrição';
			private var snapshotData:String = '';
			
			private var numSeries:uint = 0;
			private var serverName:String;
			
			[Bindable] private var dataProvider:ArrayCollection = new ArrayCollection();  

			private function init():void
			{
				// Especifico o caminho de onde vem os dados
				var dataUrl:String = 	Application.application.parameters.s +
										Application.application.parameters.p;
				serverName = dataUrl;
				
				addEventListener('dataLoaded',insertChart);

				chartType.addItem({	tipo:EAreaChart,
									label:'Área'});
				chartType.addItem({	tipo:EBarChart,
									label:'Barras'});
				chartType.addItem({	tipo:EBubbleChart,
									label:'Bubble'});
				chartType.addItem({	tipo:EColumnChart,
									label:'Colunas'});
				chartType.addItem({	tipo:ELineChart,
									label:'Linhas'});
				chartType.addItem({	tipo:EPlotChart,
									label:'Plot'});
				chartType.addItem({	tipo:EPieChart,
									label:'Setores'});

				// Nome do metodo PHP que sera executado
				var action:String = Application.application.parameters.a;

				var request:Object 	= new Object();

				// Uso uma chave encriptada pra garantir que ninguem vai ver os dados 'de graca'
				request['4a9cef46394effc1a5922d63a7bae5a4']	= '57088fb4a102f279fbbdeefba13813e848e4b209';
				var i:uint = 0;
				
				request['action'] = action;
				HTTPUtil.loadData(dataUrl,request,resultListener);

			}
			private function resultListener(event:ResultEvent):void
			{
				
				chartTitle = event.result.response.meta.titulo;
				chartDesc  = event.result.response.meta.desc;
				
//				fieldsMeta = event.result.response.meta.fields.row;
				fieldsMeta.removeAll();
				if(event.result.response.meta.numFields == 1)
					fieldsMeta.addItem(event.result.response.meta.fields.row);
				else if(event.result.response.meta.numFields > 1)
					fieldsMeta = event.result.response.meta.fields.row as ArrayCollection;
				
				dataProvider.removeAll();
				if(event.result.response.meta.affectedRows == 1)
					dataProvider.addItem(event.result.response.data.row);
				else if(event.result.response.meta.affectedRows > 1)
					dataProvider = event.result.response.data.row as ArrayCollection;
				
				mainAxisField = event.result.response.meta.mainAxisField;
				secondaryAxisFields.removeAll();
				if(event.result.response.meta.numFields == 2)
					secondaryAxisFields.addItem(event.result.response.meta.secondaryAxisFields.row);
				else if(event.result.response.meta.numFields > 2)
					secondaryAxisFields = event.result.response.meta.secondaryAxisFields.row as ArrayCollection;

				dispatchEvent(new Event('dataLoaded'));
				
			}

			private function insertChart(e:Event):void
			{
				vbGrafico.removeAllChildren();

				var chartType:Class = cbxTipoGrafico.selectedItem.tipo as Class;
				var chart = new chartType();

				chart.width = 600;
				chart.percentHeight = 100;
				chart.showDataTips = true; 

				chart.dataProvider = dataProvider;
				
				for each(var secondaryField:String in secondaryAxisFields)
					chart.addSeries(mainAxisField,secondaryField,findField(secondaryField));

				var lgLegenda:Legend = new Legend();
				lgLegenda.direction = 'horizontal';

				lgLegenda.dataProvider = chart;

				vbGrafico.addChild(chart);
				vbGrafico.addChild(lgLegenda);
			}
			private function findField(name:String):String
			{
				for each(var item in fieldsMeta)
					if(item.name == name)
						return item.value;
				return '';
			}
			public function upload(imageData:String):void
			{
                var request:Object = new Object();
                request['fileContents'] = imageData;
                
                HTTPUtil.loadData(serverName+'upload.php',request,printChart);
            }
            private function grabSnapshot():void
            {
            	var snapshot:ImageSnapshot = ImageSnapshot.captureImage(vbGrafico,0,new JPEGEncoder(100));
				var imageData:String = ImageSnapshot.encodeImageAsBase64(snapshot);
                upload(imageData);
            }
			private function printChart(event:ResultEvent):void
			{
				var request:URLVariables = new URLVariables();
				request.image = event.result.response.filename;
				request.title = chartTitle;
				request.desc = chartDesc;
				
				for(var i:String in dataProvider.source)
				{
					for(var j:String in dataProvider.source[i])
					{
						request['params[data]['+i+']['+j+']'] = dataProvider.source[i][j];
					}
				}
				for(i in fieldsMeta.source)
				{
					for(j in fieldsMeta.source[i])
						request['params[headers]['+i+']['+j+']'] = fieldsMeta.source[i][j];
				}
				var urlRequest:URLRequest = new URLRequest(serverName+'print.php');
				
				urlRequest.method = URLRequestMethod.POST;
				urlRequest.data = request;
				navigateToURL(urlRequest,'_blank');
			}

			private function filter():void
			{
				dataProvider.filterFunction = filterFunction;
				dataProvider.refresh();
			}

			// Funcao que filtra o grafico
			private function filterFunction(row:Object):Boolean
			{
				var prop:String = cbxFiltro.selectedItem.name;
				var expression:Boolean = true;
				if(!row.hasOwnProperty(prop))
					return true;
				if(txtF.text != '')
					expression = (expression && row[prop] <= parseInt(txtF.text));
				if(txtFZero.text != '')
					expression = (expression && row[prop] >= parseInt(txtFZero.text));
				return expression;
			}
		]]>
	</mx:Script>
	
	<mx:Fade alphaFrom="0" alphaTo="1" id="fxFade" duration="500" />
	
	<mx:VBox width="100%" verticalAlign="middle">
		<mx:Label text="{chartTitle}" fontWeight="bold" fontSize="19" textAlign="left"/>
		<mx:Label text="{chartDesc}" fontWeight="normal" fontSize="14"/>
	</mx:VBox>
	<mx:VBox width="100%" height="100%" horizontalAlign="left" verticalAlign="middle" horizontalScrollPolicy="off">
		<mx:HBox width="100%" height="100%">
			<mx:VBox id="vbGrafico" width="600" height="100%" 
				horizontalAlign="center"
				backgroundColor="#ffffff"
				verticalAlign="middle" 
				resizeEffect="fxFade">
			</mx:VBox>
			<mx:VBox width="100%" height="100%" verticalAlign="top" horizontalAlign="left">
				<mx:Label text="Filtrar por" fontWeight="bold" fontSize="12"/>
				<mx:ComboBox id="cbxFiltro"
					labelField="value"
					dataProvider="{fieldsMeta}"
					width="100%"/>
				<mx:HBox width="100%">
					<mx:TextInput width="50" id="txtFZero" />
					<mx:Label text="a" fontWeight="bold" fontSize="12"/>
					<mx:TextInput width="50" id="txtF" />	
				</mx:HBox>
				
				<mx:Button label="Filtrar" click="filter()"/>
				<mx:HRule width="100%"/>
				<mx:Label text="Tipo de gráfico" fontWeight="bold" fontSize="12"/>
				<mx:ComboBox id="cbxTipoGrafico" 
					dataProvider="{chartType}" 
					change="insertChart(event)" 
					labelField="label" width="100%"/>
				<mx:Button label="Imprimir" click="grabSnapshot()"/>
			</mx:VBox>
		</mx:HBox>
	</mx:VBox>
</mx:Application>